PI = 3.141592653589793
SOLAR_MASS = 4.0 * PI * PI
DAYS_PER_YEAR = 365.24

fn create_body(x, y, z, vx, vy, vz, mass) {
    return [x, y, z, vx, vy, vz, mass]
}

fn body_offset_momentum(body, px, py, pz) {
    body[3] = -px / SOLAR_MASS
    body[4] = -py / SOLAR_MASS
    body[5] = -pz / SOLAR_MASS
}

fn create_jupiter() {
    return create_body(
        4.84143144246472090,
        -1.16032004402742839,
        -0.103622044471123109,
        0.00166007664274403694 * DAYS_PER_YEAR,
        0.00769901118419740425 * DAYS_PER_YEAR,
        -0.0000690460016972063023 * DAYS_PER_YEAR,
        0.000954791938424326609 * SOLAR_MASS
    )
}

fn create_saturn() {
    return create_body(
        8.34336671824457987,
        4.12479856412430479,
        -0.403523417114321381,
        -0.00276742510726862411 * DAYS_PER_YEAR,
        0.00499852801234917238 * DAYS_PER_YEAR,
        0.0000230417297573763929 * DAYS_PER_YEAR,
        0.000285885980666130812 * SOLAR_MASS
    )
}

fn create_uranus() {
    return create_body(
        12.8943695621391310,
        -15.1111514016986312,
        -0.223307578892655734,
        0.00296460137564761618 * DAYS_PER_YEAR,
        0.00237847173959480950 * DAYS_PER_YEAR,
        -0.0000296589568540237556 * DAYS_PER_YEAR,
        0.0000436624404335156298 * SOLAR_MASS
    )
}

fn create_neptune() {
    return create_body(
        15.3796971148509165,
        -25.9193146099879641,
        0.179258772950371181,
        0.00268067772490389322 * DAYS_PER_YEAR,
        0.00162824170038242295 * DAYS_PER_YEAR,
        -0.0000951592254519715870 * DAYS_PER_YEAR,
        0.0000515138902046611451 * SOLAR_MASS
    )
}

fn create_sun() {
    return create_body(0.0, 0.0, 0.0, 0.0, 0.0, 0.0, SOLAR_MASS)
}

fn nbody_init(bodies) {
    px = 0.0
    py = 0.0
    pz = 0.0
    size = len(bodies)

    for (i = 0; i < size; i++) {
        b = bodies[i]
        m = b[6]
        px = px + b[3] * m
        py = py + b[4] * m
        pz = pz + b[5] * m
    }

    body_offset_momentum(bodies[0], px, py, pz)
}

fn nbody_advance(bodies, dt) {
    size = len(bodies)

    for (i = 0; i < size; i++) {
        bodyi = bodies[i]

        for (j = i + 1; j < size; j++) {
            bodyj = bodies[j]

            dx = bodyi[0] - bodyj[0]
            dy = bodyi[1] - bodyj[1]
            dz = bodyi[2] - bodyj[2]

            distance = sqrt(dx*dx + dy*dy + dz*dz)
            mag = dt / (distance * distance * distance)

            bodyi[3] = bodyi[3] - dx * bodyj[6] * mag
            bodyi[4] = bodyi[4] - dy * bodyj[6] * mag
            bodyi[5] = bodyi[5] - dz * bodyj[6] * mag

            bodyj[3] = bodyj[3] + dx * bodyi[6] * mag
            bodyj[4] = bodyj[4] + dy * bodyi[6] * mag
            bodyj[5] = bodyj[5] + dz * bodyi[6] * mag
        }
    }

    for (i = 0; i < size; i++) {
        body = bodies[i]
        body[0] = body[0] + dt * body[3]
        body[1] = body[1] + dt * body[4]
        body[2] = body[2] + dt * body[5]
    }
}

fn nbody_energy(bodies) {
    e = 0.0
    size = len(bodies)

    for (i = 0; i < size; i++) {
        bodyi = bodies[i]

        e = e + 0.5 * bodyi[6] * (bodyi[3] * bodyi[3] + bodyi[4] * bodyi[4] + bodyi[5] * bodyi[5])

        for (j = i + 1; j < size; j++) {
            bodyj = bodies[j]

            dx = bodyi[0] - bodyj[0]
            dy = bodyi[1] - bodyj[1]
            dz = bodyi[2] - bodyj[2]

            distance = sqrt(dx*dx + dy*dy + dz*dz)
            e = e - (bodyi[6] * bodyj[6]) / distance
        }
    }

    return e
}

fn run_nbody(n) {
    bodies = [
        create_sun(),
        create_jupiter(),
        create_saturn(),
        create_uranus(),
        create_neptune()
    ]

    nbody_init(bodies)

    max = n * 100

    energy_before = nbody_energy(bodies)

    for (i = 0; i < max; i++) {
        nbody_advance(bodies, 0.01)
    }

    energy_after = nbody_energy(bodies)

    return energy_before + energy_after
}

ret = 0.0
iterations = [3, 6, 12, 24]

for (idx = 0; idx < 4; idx++) {
    n = iterations[idx]
    print("Running n-body with n =", n)
    start = time()
    ret = ret + run_nbody(n)
    end = time()
    print("Time:", end - start, "sec")
}

expected = -1.3524862408537381
print("Result:", ret)
print("Expected:", expected)

if (ret != expected) {
    print("ERROR: bad result!")
} else {
    print("SUCCESS!")
}